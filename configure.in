AC_INIT(fribidi.c)

PACKAGE=fribidi
VERSION=0.9.1
AM_CONFIG_HEADER(config.h)
AM_INIT_AUTOMAKE($PACKAGE, $VERSION)

dnl Checks for programs.
AC_PROG_AWK
AC_PROG_CC
AC_PROG_CPP
AC_PROG_INSTALL
AC_PROG_LN_S
AC_PROG_MAKE_SET
AC_PROG_RANLIB


dnl Checks for libraries

dnl Check for glib
NO_GLIB_HEADERS=""
AC_SUBST(NO_GLIB_HEADERS)
dnl Cygwin does not expand two cannonical macros
AM_PATH_GLIB_2_0(1.2.0,
  [GLIB_CFLAGS_OLD="$GLIB_CFLAGS"
   GLIB_LIBS_OLD="$GLIB_LIBS"
   AM_PATH_GLIB_2_0(1.3.1,,
   [GLIB_CFLAGS="$GLIB_CLFAGS_OLD"
    GLIB_LIBS="$GLIB_LIBS_OLD"
   ])
  ],
  [GLIB_CFLAGS="-DFRIBIDI_USE_MINI_GLIB"
   GLIB_LIBS="-L. -lfribidi_mini_glib"
   NO_GLIB_HEADERS="fribidi_mini_glib.h"])

dnl Initialize libtool
AM_PROG_LIBTOOL

AC_SUBST(GETOPTLIB)
AC_CHECK_FUNC(getopt_long, GETOPTLIB="", GETOPTLIB="-L. -lgetopt")


dnl Checks for header files.
AC_HEADER_STDC
AC_CHECK_HEADERS(strings.h)


dnl Checks for typedefs
AC_CHECK_SIZEOF(int, 2)


dnl Checks for compiler characteristics

changequote(,)dnl
if test "x$GCC" = "xyes"; then
  case " $CFLAGS " in
  *[\ \	]-Wall[\ \	]*) ;;
  *) CFLAGS="$CFLAGS -Wall" ;;
  esac
fi
changequote([,])dnl

dnl Cygwin does not set srcdir to ".".
if test x$srcdir = x; then
  xsrcdir=.
else
  xsrcdir="$srcdir"
fi


dnl Check for configure options

dnl --enable-debug
AC_ARG_ENABLE(debug, dnl
[  --enable-debug           Turn on debugging information [default=no]],
[case "${enableval}" in
  yes) debug=true ;;  
  no)  debug=false ;;
  *) AC_MSG_ERROR(bad value ${enableval} for --enable-debug) ;;
esac],[debug=false])
if test x"$debug" = xtrue; then
  AC_DEFINE(DEBUG)
fi

dnl --enable-memopt
AC_ARG_ENABLE(memopt,
[  --enable-memopt          Turn on optimization for memory usage [default=no]],
[case "${enableval}" in
  yes) memopt=true ;;
  no)  memopt=false ;;
  *) AC_MSG_ERROR(bad value ${enableval} for --enable-memoptimized) ;;
esac],[memopt=false])
if test x"$memopt" = xtrue; then
  AC_DEFINE(MEM_OPTIMIZED)
fi

dnl --enable-malloc
AC_ARG_ENABLE(malloc,
[  --enable-malloc          Use malloc() instead of glib functions [default=no]],
[case "${enableval}" in
  yes) malloc=true ;;
  no)  malloc=false ;;
  *) AC_MSG_ERROR(bad value ${enableval} for --enable-malloc) ;;
esac],[malloc=false])
if test x"$malloc" = xtrue; then
  AC_DEFINE(USE_SIMPLE_MALLOC)
fi


AC_OUTPUT([
Makefile
fribidi-config
],[case "$CONFIG_FILES" in
*fribidi-config*)chmod +x fribidi-config;;
esac])
